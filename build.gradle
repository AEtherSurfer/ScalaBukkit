apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'idea'

eclipse {
	classpath {
		downloadSources=true
	}
}

ext {
	scalaVersion = '2.11.7'
	buildNumber = "DEV"
}

group = "com.aethercraft"
version = "$scalaVersion-R0.3.0-SNAPSHOT"

sourceCompatibility = 1.7

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		name 'scala-releases'
		url 'http://scala-tools.org/repo-releases/'
	}
	maven {
		name 'scala-snapshots'
		url 'http://scala-tools.org/repo-snapshots/'
	}
	maven {
		url "http://repo.bukkit.org/content/groups/public"
	}
}

configurations {
	scalaCompiler
	scalaLibrary
    provided
}

tasks.withType(ScalaCompile) {
	scalaCompileOptions.useCompileDaemon = false
	scalaCompileOptions.useAnt = false
	scalaCompileOptions.additionalParameters = ['-target:jvm-1.7']
}

dependencies {
	scalaCompiler "org.scala-lang:scala-compiler:$scalaVersion"
	scalaLibrary "org.scala-lang:scala-library:$scalaVersion"

    compile "org.scala-lang:scala-library:$scalaVersion"

	provided 'org.bukkit:bukkit:1.6.2-R1.0'
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

processResources {
	filter {
		line ->
		line
		.replace('${name}', project.name)
		.replace('${version}', project.version)
		.replace('${buildNumber}', project.ext.buildNumber)
		.replace('${pluginMainClass}', "${project.group}.${project.name.toLowerCase()}.Plugin")
	}
}

//include dependencies for a "fat jar"
//http://docs.codehaus.org/display/GRADLE/Cookbook#Cookbook-Creatingafatjar
jar {
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.11'
}
